
class JackAstToC {
	field File dst;
	field JackParser parser;
	field JackAst ast;
	field JackAst current;
	field String clas;
	field int nb_field;
	field int nb_static;
	field boolean has_callback;
	field int sp;
	field int stringid;

	constructor JackAstToC new(JackParser parse) {
		let parser = parse;
		let dst = null;
		let ast = parse.getAst();
		return this;
	}

	method void dispose() {
		do Memory.deAlloc(this);
		return;
	}
	
	method boolean compile(File dst_) {
		var JackAst c;
		let dst = dst_;
		let current = ast;

		do nl();
		do dst.writeString("#if 0");
		do nl();
		//do ast.process(dst, 0, parser);
		do nl();
		do dst.writeString("#endif");
		do nl();

		let stringid = 0;
		let sp = 0;
		let nb_field = 0;
		let nb_static = 0;
		let has_callback = false;
		do dst.writeString("#ifndef var");
		do nl();
		do dst.writeString("#ifdef _WIN64");
		do nl();
		do dst.writeString("#include <windows.h>");
		do nl();
		do dst.writeString("#define var __int64");
		do nl();
		do dst.writeString("#else");
		do nl();
		do dst.writeString("#define var long");
		do nl();
		do dst.writeString("#endif");
		do nl();
		do dst.writeString("#endif");
		do nl();

		while (current) {
			let c = current.getChild();
			if (c) {
				if (c.getTag() = C.CLASS()) {
					do coClass(c);
				}
			}
			let current = current.getNext();
		}
		
		return true;
	}

	method void coClass(JackAst c) {
		var JackAst d;
		var int t;
		let clas = c.getData();

		do dst.writeString("#ifndef JACK_");
		do dst.writeString(clas);
		do dst.writeString("_H");
		do nl();
		do dst.writeString("#define JACK_");
		do dst.writeString(clas);
		do dst.writeString("_H");
		do nl();

		let d = c.getChild();
		while (d) {
			let t = d.getTag();

			if (t = C.FUNCTION()) {
				do coFunctionPre(d);
			} else { if (t = C.METHOD()) {
				do coMethodPre(d);
			} else { if (t = C.CONSTRUCTOR()) {
				do coConstructorPre(d);
			}}}

			let d = d.getNext();
		}
	

		do dst.writeString("#endif");
		do nl();
		do dst.writeString("#ifdef JACK_IMPLEMENTATION");
		do nl();
		
		let d = c.getChild();
		if (d) {
			do genStrings(d);
		}
		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (t = C.FUNCTION()) {
				do coFunction(d);
			} else { if (t = C.METHOD()) {
				do coMethod(d);
			} else { if (t = C.CONSTRUCTOR()) {
				do coConstructor(d);
			} else { if (t = C.CALLBACK()) {
				do coCallback(d);
			} else { if (t = C.FIELD()) {
				do coField(d);
			} else { if (t = C.STATIC()) {
				do coStatic(d);
			} else { 
				do error("Unexpected kind.", d);
			}}}}}}
			let d = d.getNext();
		}

		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (t = C.FIELD()) {
				do coFieldUndef(d);
			}
			let d = d.getNext();
		}
		do dst.writeString("#endif");
		do nl();
	
		//do c.process(dst, 0, parser);

		return;
	}

	method void genStrings(JackAst a) {
		var String s, o;
		var int i, l, n;
		if (a.getTag() = C.STRING_CONST()) {
			do dst.writeString("var ");
			do dst.writeString(clas);
			do dst.writeString("___str");
			let s = String.new(8);
			let s = s.setInt(stringid);
			do dst.writeString(s);
			do dst.writeString("[] = {-");
			let o = a.getData();
			let l = o.length();
			let i = 0;
			let s = s.setInt((l+2));
			do dst.writeString(s);
			while (i < l) {
				do dst.writeString(",");
				let n = o.charAt(i);
				let s = s.setInt(n);
				do dst.writeString(s);
				let i = i + 1;
			}
			do dst.writeString(",0};");
			do nl();
			do s.dispose();
			do a.setId(stringid);
			let stringid = stringid + 1;
		}
		if (a.getLeft()) {
			do genStrings(a.getLeft());
		}
		if (a.getChild()) {
			do genStrings(a.getChild());
		}
		if (a.getNext()) {
			do genStrings(a.getNext());
		}
		return;
	}

	method void spc() {
		var int i;
		let i = 0;
		while (i < sp) {
			let i = i + 1;
			do dst.writeString("	");
		}
		return;
	}

	
	method String str(int c) {
		return parser.getConst(c);
	}

	method void nl() {
		do dst.writeByte(10);
		return;
	}

	method void coAsm(JackAst c) {
		var String s;
		var String z;
		var int i;
		var int l;
		let i = 0;
		let z = "c";
		let s = c.getData();
		let l = s.length();
		while (s.charAt(i) = z.charAt(i)) {
			let i = i + 1;
			if (i = l) {
				return;
			} 
			if (i = z.length()) {
				while (i < l) {
					do dst.writeUtf8(s.charAt(i));	
					let i = i + 1;
				}
				return;
			}		
		}
		return;
	}

	method void coStatic(JackAst c) {
		let c = c.getChild();
		//do coType(c);
		do dst.writeString("var ");
		do dst.writeString(clas);
		do dst.writeString("__");
		let c = c.getChild();
		do coVarName(c);
		do dst.writeString(";");
		do nl();
		let nb_static = nb_static + 1;
		return;
	}

	method void coFieldUndef(JackAst c) {
		let c = c.getChild();
		do dst.writeString("#undef ");
		let c = c.getChild();
		do coVarName(c);
		do nl();
		return;
	}

	method void coField(JackAst c) {
		var String s;
		let c = c.getChild();
		do dst.writeString("#define ");
		let c = c.getChild();
		do coVarName(c);
		do dst.writeString(" ((var*)__this)[");
		let s = String.new(8);
		let s = s.setInt(nb_field);
		do dst.writeString(s);
		do s.dispose();
		do dst.writeString("]");
		do nl();
	
		let nb_field = nb_field + 1;
		return;
	}

	method void coMethodPre(JackAst c) {
		var JackAst d;
		var int t;

		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, true);
				do dst.writeString(";");
				do nl();
				return;
			}	
			let d = d.getNext();
		}
		return;
	}

	method void coConstructorPre(JackAst c) {
		var JackAst d;
		var int t;

		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, false);
				do dst.writeString(";");
				do nl();
				return;
			}	
			let d = d.getNext();
		}
		return;
	}


	method void coMethod(JackAst c) {
		var JackAst d;
		var int t, l;

		let d = c.getChild();
		let l = 0;
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, true);
				do dst.writeString(" {");
				do nl();
				let sp = sp + 1;
			}	
			if (t = C.VAR()) {
				do coVarDec(d);
				let l = l + 1;
			}
			if (t = C.STATEMENTS()) {
				do coStatements(d);
				let sp = sp - 1;
				do dst.writeString("}");
				do nl();
			}
			let d = d.getNext();
		}
		return;
	}

	method void coConstructor(JackAst c) {
		var JackAst d;
		var int t, l;
		var String s;

		let d = c.getChild();
		let l = 0;
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, false);
				do dst.writeString(" {");
				do nl();
				let sp = sp + 1;
			}	
			if (t = C.VAR()) {
				if (l = 0) {
					do spc();
					do dst.writeString("var __this;");
					do nl();
				}
				do coVarDec(d);
				let l = l + 1;
			}
			if (t = C.STATEMENTS()) {
				let t = t;
				if (l = 0) {
					do spc();
					do dst.writeString("var __this;");
					do nl();
				}
				do spc();
				do dst.writeString("__this = Memory__alloc(");
				let s = String.new(8);
				if (nb_field < 1) {
					let s = s.setInt(1);
				} else {
					let s = s.setInt(nb_field);
				}
				do dst.writeString(s);
				do s.dispose();
				do dst.writeString(");");
				do nl();
				if (has_callback) {
					do spc();
					do dst.writeString(
						"((var*)__this)[0] = (var)");
					do dst.writeString(clas);
					do dst.writeString("__callback;");
					do nl();
				}
				do coStatements(d);
				let sp = sp - 1;
				do dst.writeString("}");
				do nl();
			}
			let d = d.getNext();
		}
		return;
	}

	method void coCallback(JackAst c) {
		let nb_field = nb_field + 1;
		let has_callback = 1;
		do coMethod(c);
		return;
	}

	method void coFunctionPre(JackAst c) {
		var JackAst d;
		var int t;

		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, false);
				do dst.writeString(";");
				do nl();
				return;
			}	
			let d = d.getNext();
		}
		return;
	}

	method boolean isType(int t) {
		if ((t = C.CLASSNAME()) | 
			(t = C.VOID()) |
			(t = C.INT()) |
			(t = C.BOOLEAN()) |
			(t = C.CHAR()))
		{
			return true;
		}
		return false;
	}

	method void coFunction(JackAst c) {
		var JackAst d;
		var int t;

		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				do coFunctionHead(d, false);
				do dst.writeString(" {");
				do nl();
				let sp = sp + 1;
			}	
			if (t = C.VAR()) {
				do coVarDec(d);
			}
			if (t = C.STATEMENTS()) {
				let t = t;
				do coStatements(d);
				let sp = sp - 1;
				do dst.writeString("}");
				do nl();
			}
			let d = d.getNext();
		}
		return;
	}

	method void error(String txt, JackAst c) {
		if (txt.length() < 1) {
			return;
		}
		do Output.printString("In ");
		if (c) {
			do Output.printString(c.getFile());
		} else {
			do Output.printString(clas);
		}
		do Output.printString(" (");
		if (c) {
			do Output.printInt(c.getLine());
		}
		do Output.printString(") ");
		do Output.printString(txt);
		do Output.printString(" got ");
		if (c) {
			do Output.printString(str(c.getTag()));
			do Output.printString(" '");
			if (c.getData()) {
				do Output.printString(c.getData());
			}
			do Output.printString("'.");
		} else {
			do Output.printString(" null ");
		}
		do Output.println();
		//let c = 1/0;
		return;
	}

	method void coStatement(JackAst c) {
		var int t;
		let t = c.getTag();
		if (t = C.ASSIGN()) {
			do coAssign(c);
		} else { if (t = C.WHILE()) {
			do coWhile(c);
		} else { if (t = C.IF()) {
			do coIf(c);
		} else { if (t = C.SUBROUTINECALL()) {
			do spc();
			do coSubroutineCall(c);
			do dst.writeString(";");
		} else { if (t = C.RETURN()) {
			do coReturn(c);
		} else { if (t = C.ASM()) {
			do coAsm(c);
		} else {
			do error("Unknown statement", c);
		}}}}}}
		do nl();
		return;
	}

	method void coWhile(JackAst c) {
		var JackAst d;
		let d = c.getChild();
		do spc();
		do dst.writeString("while (");
		if (d) {
			do coExpr(d, false);
		} else {
			do error("expected expression in while", c);
			return;
		}
		do dst.writeString(") {");
		do nl();
		let sp = sp + 1;
		let d = d.getNext();
		do coStatements(d);
		let sp = sp - 1;
		let d = d.getNext();
		do spc();
		do dst.writeString("}");
		return;
	}

	method void coIf(JackAst c) {
		var JackAst d;
		let d = c.getChild();
		do spc();
		do dst.writeString("if (");
		if (d) {
			do coExpr(d, false);
		} else {
			do error("expected expression in if", c);
			return;
		}
		do dst.writeString(") {");
		do nl();
		let sp = sp + 1;
		let d = d.getNext();
		if (d = 0) {
			do error("expected statements in if", c);
			return;
		}
		if ((d.getTag() = C.STATEMENTS()) = false) {
			do error("expected statements.", d);
			return;
		}
		do coStatements(d);
		let d = d.getNext();
		let sp = sp - 1;
		if (d = 0) {
			do spc();
			do dst.writeString("}");
			do nl();
			return;
			
		}
		if ((d.getTag() = C.ELSE()) = false) {
			do error("expected else", c);
			return;
		}
		do spc();
		do dst.writeString("} else {");
		do nl();
		let sp = sp + 1;
		let c = d;
		let d = d.getChild();
		if (d = 0) {
			do error("expected statements in else", c);
			return;
		}
		do coStatements(d);
		let sp = sp - 1;
		do spc();
		do dst.writeString("}");
		return;
	}



	method void coReturn(JackAst c) {
		var JackAst d;
		let d = c.getChild();
		do spc();
		do dst.writeString("return ");
		if (d) {
			do coExpr(d, false);
		} else {
			do dst.writeString("0");
		}
		do dst.writeString(";");
		return;
	}

	method void coAssign(JackAst c) {
		var JackAst d;
		let d = c.getLeft();
		if (d = null) {
			return;
		}
		do spc();
		do coVarNameOrIndexed(d);
		let d = c.getChild();
		if (d) {
			do dst.writeString(" = ");
			do coExpr(d, false);
		}
		do dst.writeString(";");
		return;
	}

	method void coVarNameOrIndexed(JackAst c) {
		var JackAst d;
		let d = c.getChild();
		if (d) {
			do dst.writeString("((var*)");
			do coVarName(c);
			do dst.writeString(")");
			if (d.getTag() = C.LEFT_SQUARE_BRACKET()) {
				do dst.writeString("[");
				let d = d.getChild();
				if (d.getTag() = C.EXPR()) {
					do coExpr(d, false);
				}
				do dst.writeString("]");
			} else {
				do error("'[' expected", d);
				return;
			}
		} else {
			do coVarName(c);
		}
		return;
	}

	method void coExpr(JackAst c, boolean isterm) {
		var JackAst d;
		var boolean ok;
		var int t;
		var String s;

		if (c.getTag() = C.EXPR()) {
			let d = c.getChild();
		} else {
			let d = c;
		}
		while (d) {
			let ok = false;
			let t = d.getTag();
			if (t = C.SUBROUTINECALL()) {
				do coSubroutineCall(d);
				let ok = true;
			}
			if (t = C.UNARYMINUS()) {
				do dst.writeString("-");
				do coExpr(d.getChild(), true);
				let ok = true;
			}
			if (t = C.UNARYTILDE()) {
				do dst.writeString("~");
				do coExpr(d.getChild(), true);
				let ok = true;
			}
			if (t = C.NULL()) {
				do dst.writeString("0");
				let ok = true;
			}
			if (t = C.THIS()) {
				do dst.writeString("__this");
				let ok = true;
			}
			if (t = C.FALSE()) {
				do dst.writeString("0");
				let ok = true;
			}
			if (t = C.TRUE()) {
				do dst.writeString("-1");
				let ok = true;
			}
			if (t = C.LEFT_PARENTHESIS()) {
				do dst.writeString("(");
				do coExpr(d.getChild(), false);
				do dst.writeString(")");
				let ok = true;
			}
			if (t = C.INT_CONST()) {
				do dst.writeString(d.getData());
				let ok = true;
			}
			if (t = C.STRING_CONST()) {
				do dst.writeString("((var)(");
				do dst.writeString(clas);
				do dst.writeString("___str");
				let s = String.new(8);
				let s = s.setInt(d.getId());
				do dst.writeString(s);
				do s.dispose();
				let stringid = stringid + 1;
				do dst.writeString("+1))");
				/*
				do dst.writeByte(34);
				do dst.writeString(d.getData());
				do dst.writeByte(34);
				*/
				let ok = true;
			}
			if (t = C.PLUS()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("+");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.MINUS()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("-");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.MULTIPLY()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("*");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.DIV()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("/");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.AND()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("&");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.OR()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("|");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.LESS()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("<");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.GREATER()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString(">");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.EQUAL()) {
				do coExpr(d.getLeft(), false);
				do dst.writeString("==");
				do coExpr(d.getChild(), false);
				let ok = true;
			}

			if (t = C.VARNAME()) {
				do coVarNameOrIndexed(d);
				let ok = true;
			}
			if (t = C.EXPR()) {
				if ((d = c) = false) {
					do coExpr(d, false);
				}
				let ok = true;
			}
			if (ok = false) {
				do error("Unexpected term", d);
			}
			if (isterm) {
				return;
			}
			let d = d.getNext();
		}
		return;
	}

	method boolean isSubroutine(int t) {
		if (t = C.FUNCTION()) {
			return true;
		}
		if (t = C.METHOD()) {
			return true;
		}
		if (t = C.CONSTRUCTOR()) {
			return true;
		}
		if (t = C.CALLBACK()) {
			return true;
		}
		return false;
	}

	method boolean isVarnameSet(JackAst c, String s) {
		var JackAst d;
		let d = c.getChild();
		if (d) {
			// type
			let d = d.getChild();
		}
		if (d) {
			if (s.compare(d.getData()) = 0) {
				return true;					
			}
		}
		return false;
	}


	method String getTypeOfVarname(JackAst c, String s) {
		var JackAst d;
		var int t;
		let d = c.getChild();
		if (d = null) {
			return null;
		}
		if (d.getData() = null) {
			return null;
		}
		if (s.compare(d.getData()) = 0) {
			let t = c.getTag();
			if ((t = C.VOID()) |		
				(t = C.INT()) |		
				(t = C.BOOLEAN()) |		
				(t = C.CHAR()))
			{
				return str(t);
			}
			return c.getData();					
		}
		return null;
	}

	method String functionGetClassOf(JackAst c, String s) {
		var JackAst d, n;
		var int t;
		var String r;
		let d = c.getChild();
		let t = d.getTag();
		if (isType(t)) {
			let n = d.getChild();
			let t = n.getTag();
			if (t = C.SUBROUTINENAME()) {
				let n = n.getChild();
				let t = n.getTag();
				if (t = C.PARAMS()) {
				    let n = n.getChild();
				    while (n) {
					let r = isVarnameSet(n, s); 
					if (r) {
					    return "";
					}
					let n = n.getNext();
				    }
				}
			
			}	
		}
		while (d) {
			let t = d.getTag();
			if (t = C.VAR()) {
				let n = d.getChild();
				if (n) {
					let r = isVarnameSet(n, s); 
					if (r) {
						return "";
					}
				}
			}
			let d = d.getNext(); 
		}
		return null;	
	}


	method String functionGetTypeOf(JackAst c, String s) {
		var JackAst d, n;
		var int t;
		var String r;
		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if (isType(t)) {
				let n = d.getChild();
				let t = n.getTag();
				if (t = C.SUBROUTINENAME()) {
					let n = n.getChild();
					let t = n.getTag();
					if (t = C.PARAMS()) {
					    let n = n.getChild();
					    while (n) {
						let r = getTypeOfVarname(n, s); 
						if (r) {
						    return r;
						}
						let n = n.getNext();
					    }
					}
				
				}	
			}
			if (t = C.VAR()) {
				let n = d.getChild();
				if (n) {
					let r = getTypeOfVarname(n, s); 
					if (r) {
						return r;
					}
				}
			}
			let d = d.getNext(); 
		}
		return null;	
	}

	method String classGetClassOf(JackAst c, String s) {
		var JackAst d;
		var int t;
		var String r;
		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if ((t = C.FIELD()) | (t = C.STATIC())) {
				let r = isVarnameSet(d, s); 
				if (r) {
					return clas;
				}
			}
			let d = d.getNext();
		}

		return null;
	}


	method String classGetTypeOf(JackAst c, String s) {
		var JackAst d;
		var int t;
		var String r;
		let d = c.getChild();
		while (d) {
			let t = d.getTag();
			if ((t = C.FIELD()) | (t = C.STATIC())) {
				let r = getTypeOfVarname(d.getChild(), s); 
				if (r) {
					return r;
				}
			}
			let d = d.getNext();
		}

		return null;
	}

	method String getTypeOf(JackAst c) {
		var JackAst d;
		var int t;
		var String s, r;
		let s = c.getData();
		let d = c.getParent();
		let t = d.getTag();
		while (((d=null)=false) & (isSubroutine(t) = false)) {
			let d = d.getParent();
			if (d) {
				let t = d.getTag();
			}
		}
		if (d = null) {
			return null;
		}

		let r = functionGetTypeOf(d, s);
		if (r) {
			return r;
		}	
		while (((d = null) = false) & ((t = C.CLASS()) = false)) {
			let d = d.getParent();
			if (d) {
				let t = d.getTag();
			}
		}
		if (d = null) {
			return null;
		}

		let r = classGetTypeOf(d, s);
		if (r) {
			return r;
		}	

		return null;
	}


	method boolean isField(JackAst c) {
		var JackAst d, n;
		var int t;
		var String s;
		let s = c.getData();
		let d = c.getParent();
		let t = d.getTag();
		while (((d = null) = false) & ((t = C.CLASS()) = false)) {
			let d = d.getParent();
			if (d) {
				let t = d.getTag();
			}
		}
		if (d = null) {
			return false;
		}
		let d = d.getChild();
		while (d) {
			if (d.getTag() = C.FIELD()) {
				let n = d.getChild();
				if (n) {
					let n = n.getChild();
					if (s.compare(n.getData()) = 0) {
						return true;
					}
				}
			}
			let d = d.getNext();
		}

		return false;
	}

	method String getClassOf(JackAst c) {
		var JackAst d;
		var int t;
		var String s, r;
		let s = c.getData();
		let d = c.getParent();
		let t = d.getTag();
		while (((d = null) = false) & (isSubroutine(t) = false)) {
			let d = d.getParent();
			if (d) {
				let t = d.getTag();
			}
		}
		if (d = null) {
			return null;
		}

		let r = functionGetClassOf(d, s);
		if (r) {
			return r;
		}	
		while (((d = null) = false) & ((t = C.CLASS()) = false)) {
			let d = d.getParent();
			if (d) {
				let t = d.getTag();
			}
		}
		if (d = null) {
			return null;
		}

		let r = classGetClassOf(d, s);
		if (r) {
			return r;
		}	

		return null;
	}

	method void coSubroutineCall(JackAst c) {
		var JackAst d;
		var int t;
		var String s;
		var String thi;
		var boolean first;
		var boolean doit;
		let first = true;
		let thi = null;
		let d = c.getChild();
		let t = d.getTag();
		if ((t = C.CLASSORVARNAME())) {
			let s = getTypeOf(d);
			let doit = false;
			if (s) {
				if (s.length() > 0) {
					let doit = true;
				}
			}
			if (doit) {
				let thi = d.getData();
				do dst.writeString(s);	
			} else {
				do dst.writeString(d.getData());	
			}
			let d = d.getChild();
			do dst.writeString("__");	
			do dst.writeString(d.getData());
		} else { if ((t = C.CLASSNAME())) {
			do dst.writeString(d.getData());
			let d = d.getChild();
			do dst.writeString("__");	
			do dst.writeString(d.getData());
		} else { if (t = C.CALLBACK()) {
			do dst.writeString(
				"((var(*)(var,var,var))((var*)__this)[0])");	
			let thi = "__this";
		} else { if (t = C.SUBROUTINENAME()) {
			do dst.writeString(clas);	
			do dst.writeString("__");	
			do dst.writeString(d.getData());	
			let thi = "__this";
		}}}}
		do dst.writeString("(");
		if (thi) {
			let first = false;
			do dst.writeString(thi);
		}	
		let d = d.getChild();
		while (d) {
			if (first = false) {
				do dst.writeString(", ");
			}
			let first = false;
			do coExpr(d, false);
			let d = d.getNext();
		}	
		do dst.writeString(")");	
		return;
	}

	method void coStatements(JackAst c) {
		var JackAst d;
		let d = c.getChild();
		while (d) {
			do coStatement(d);
			let d = d.getNext();
		}
		return;
	}

	method void coFunctionHead(JackAst c, boolean add_this) {
		var JackAst d;
		var int t;
		let d = c.getChild();
		let t = c.getTag();
		if (d=null) {
			return;
		}
		if (t = C.CLASSNAME()) {
		//	do dst.writeString(c.getData());
			do dst.writeString("var");
		} else {
			do dst.writeString("var");
		}
		do dst.writeString(" ");
		do dst.writeString(clas);
		do dst.writeString("__");
		if (d.getTag() = C.SUBROUTINENAME()) {
			do dst.writeString(d.getData());
			let d = d.getChild();
		} else {
			do error("subroutine name expected", c);
			return;
		}
		if (d.getTag() = C.PARAMS()) { 
			do coParams(d, add_this);
		}
		return;
	}
	
	method void coParams(JackAst d, boolean add_this) {
		var JackAst n;
		do dst.writeString("(");
		let d = d.getChild();
		if (add_this) {
			do dst.writeString("var __this");
			if (d) {
				do dst.writeString(", ");
			}
		}
		while (d) {
			do coType(d);
			do dst.writeString(" ");
			let n = d.getChild();
			do dst.writeString(n.getData());
			let d = d.getNext();
			if (d) {
				do dst.writeString(", ");
			}
		}
		do dst.writeString(")");
		return;
	}	

	method void coVarDec(JackAst c) {
		var JackAst d, n;
		let d = c.getChild();
		while (d) {
			do spc();
			do coType(d);
			let n = d.getChild();
			if (n) {
				do dst.writeString(" ");
				do coVarName(n);
				do dst.writeString(";");
				do nl();
			}
			let d = d.getNext();
		}
		return;
	}

	method void coType(JackAst c) {
		var int t;
		let t = c.getTag();
		if (t = C.CLASSNAME()) {
			//do dst.writeString(c.getData());
			do dst.writeString("var");
		} else { 
			do dst.writeString("var");
		}
		return;
	}

	method void coVarName(JackAst c) {
		var String s;
		if (c.getTag() = C.VARNAME()) {
			if (isField(c) = false) {
				let s = getClassOf(c);
				if (s) {
					if (s.length() > 0) {
						do dst.writeString(s);
						do dst.writeString("__");
					}
				}
			}
			do dst.writeString(c.getData());
		}
		return;
	}
}
